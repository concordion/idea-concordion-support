buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.1.10'
    id 'net.saliman.cobertura' version '2.3.1'
}

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'net.saliman.cobertura'

group 'org.concordion.plugin.idea'
version '0.10.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

wrapper {
    gradleVersion = '2.12'
}

repositories {
    mavenLocal()
    mavenCentral()
}

intellij {
    pluginName 'IdeaConcordionSupport'
    plugins = ['junit', 'properties', 'Groovy', 'org.intellij.scala:2016.3.3', 'git4idea']
    version '2016.3'
    sandboxDirectory "$projectDir/idea-sandbox"
    downloadSources false
    updateSinceUntilBuild false
}

ext {
    //2016.3.20161109
    markdownSupportPlugin = files(
            "${projectDir}/markdownSupport/lib/markdown.jar",
            "${projectDir}/markdownSupport/lib/intellij-markdown.jar",
            "${projectDir}/markdownSupport/lib/Loboevolution.jar"
    )
    //not available
    markdownPlugin = files(
            "${projectDir}/markdown/lib/asm-4.1-repacked.jar",
            "${projectDir}/markdown/lib/asm-analysis-4.1-repacked.jar",
            "${projectDir}/markdown/lib/asm-tree-4.1-repacked.jar",
            "${projectDir}/markdown/lib/asm-util-4.1-repacked.jar",
            "${projectDir}/markdown/lib/idea-markdown-repacked.jar",
            "${projectDir}/markdown/lib/parboiled-core-1.1.6-repacked.jar",
            "${projectDir}/markdown/lib/parboiled-java-1.1.6-repacked.jar",
            "${projectDir}/markdown/lib/pegdown-1.4.2-repacked.jar"
    )
    //2.0.0
    markdownNavigatorPlugin = files(
            "${projectDir}/markdownNavigator/lib/asm-all-asm4.jar",
            "${projectDir}/markdownNavigator/lib/autolink-0.5.0.jar",
            "${projectDir}/markdownNavigator/lib/commons-io-2.4.jar",
            "${projectDir}/markdownNavigator/lib/commons-validator-1.4.1.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-abbreviation.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-anchorlink.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-autolink.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-definition.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-emoji.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-escaped-character.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-footnotes.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-gfm-strikethrough.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-gfm-tables.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-gfm-tasklist.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-jekyll-front-matter.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-spec-example.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-tables.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-toc.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-typographic.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-wikilink.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-yaml-front-matter.jar",
            "${projectDir}/markdownNavigator/lib/flexmark.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-test-util.jar",
            "${projectDir}/markdownNavigator/lib/idea-multimarkdown.jar",
            "${projectDir}/markdownNavigator/lib/idea-multimarkdown-javafx.jar",
            "${projectDir}/markdownNavigator/lib/javax.json-1.0.4.jar",
            "${projectDir}/markdownNavigator/lib/kotlin-reflect.jar",
            "${projectDir}/markdownNavigator/lib/kotlin-runtime.jar",
            "${projectDir}/markdownNavigator/lib/Loboevolution.jar",
            "${projectDir}/markdownNavigator/lib/parboiled-core-asm4.jar",
            "${projectDir}/markdownNavigator/lib/parboiled-java-asm4.jar",
            "${projectDir}/markdownNavigator/lib/pegdown-1.6.7.jar",
            "${projectDir}/markdownNavigator/lib/smart-data.jar"
    )
}

configurations {
    compileWith
}

sourceSets {
    main.compileClasspath += configurations.compileWith

    main.java.srcDirs = ['gen', 'src']
    main.resources.srcDirs = ['resources']
    test.java.srcDirs = ['tests']

    testMd.java.srcDirs = ['tests', 'testsMd']
    testMd.compileClasspath += sourceSets.test.compileClasspath
}

dependencies {
    compileWith markdownSupportPlugin
    compileWith markdownPlugin
    compileWith markdownNavigatorPlugin

    testCompile 'org.assertj:assertj-core:3.1.0'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

task testMarkdownSupportIntegration(type: Test) {
    classpath += markdownSupportPlugin
    classpath += sourceSets.testMd.compileClasspath
    classpath += sourceSets.testMd.runtimeClasspath
    testClassesDir = sourceSets.testMd.output.classesDir
}

task testMarkdownIntegration(type: Test) {
    classpath += markdownPlugin
    classpath += sourceSets.testMd.compileClasspath
    classpath += sourceSets.testMd.runtimeClasspath
    testClassesDir = sourceSets.testMd.output.classesDir
}

task testMarkdownNavigatorIntegration(type: Test) {
    classpath += markdownNavigatorPlugin
    classpath += sourceSets.testMd.compileClasspath
    classpath += sourceSets.testMd.runtimeClasspath
    testClassesDir = sourceSets.testMd.output.classesDir
}

check.dependsOn testMarkdownSupportIntegration
check.dependsOn testMarkdownIntegration
check.dependsOn testMarkdownNavigatorIntegration
